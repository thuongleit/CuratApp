apply plugin: 'com.android.application'

apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'io.fabric'
apply from: '../config/quality.gradle'
apply from: '../config/dependencies.gradle'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion app.compileSdk
    buildToolsVersion app.buildTools

    compileOptions.encoding = 'UTF-8' // write your encoding here

    defaultConfig {
        applicationId app.applicationId
        vectorDrawables.useSupportLibrary = true
        minSdkVersion app.minSdk
        targetSdkVersion app.targetSdk
        versionCode app.code
        versionName app.name

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        testApplicationId 'com.avectris.curatapp.test'
    }
    signingConfigs {
        release {
            storeFile file('/Volumes/Work/curatapp.jks')
            storePassword 'curatapp123'
            keyAlias "Curatapp"
            keyPassword 'curatapp123'
        }
        debug {
            storeFile rootProject.file('config/debug.keystore')
            storePassword 'android'
            keyAlias 'android'
            keyPassword 'android'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFile getDefaultProguardFile('proguard-android.txt')
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }

    sourceSets {
        release {
            java.srcDirs = ['src/release/java'];
        }
        debug {
            java.srcDirs = ['src/debug/java'];
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    lintOptions {
        quiet true
        abortOnError false
        ignoreWarnings true
        disable 'InvalidPackage'            //Some libraries have issues with this.
        disable 'OldTargetApi'
        //Lint gives this warning but SDK 20 would be Android L Beta.
        disable 'IconDensities'             //For testing purpose. This is safe to remove.
        disable 'IconMissingDensityFolder'  //For testing purpose. This is safe to remove.
    }

    retrolambda {
        jvmArgs '-noverify'
    }
}

dependencies {
    //google service
    compile "com.google.firebase:firebase-messaging:9.0.2"
    compile libraries.googleAnalytics

    // Reactive
    compile libraries.rxJava
    compile libraries.rxAndroid

    // Dagger 2
    compile libraries.dagger
    apt libraries.daggerCompiler
    provided libraries.annotation

    // Retrofit
    compile libraries.retrofit
    compile libraries.retrofitJacksonConverter
    compile libraries.retrofitRxJavaAdapter

    //support libraries
    compile libraries.supportAppCompat
    compile libraries.supportDesign
    compile libraries.supportRecyclerView
    compile libraries.supportCardView

    //utils
    compile libraries.butterKnife
    compile libraries.timber

    // saving view state
    compile libraries.icepick
    apt libraries.icepickCompiler

    //DBFlow
    compile libraries.dbFlowCore
    compile libraries.dbFlow
    apt libraries.dbFlowProcessor

    compile(libraries.crashlytics) {
        transitive = true;
    }
    compile libraries.rxBinding
    compile libraries.materialishProgress

    compile 'com.danikula:videocache:2.3.4'
    compile 'com.tbruyelle.rxpermissions:rxpermissions:0.7.0@aar'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    compile 'com.vanniktech:emoji:0.2.0'

    // debug tools
    debugCompile libraries.okHttpLoggingInterceptor
    debugCompile libraries.stetho
    debugCompile libraries.stethoOkHttp
    debugCompile libraries.leakCanary

    testCompile libraries.junit
    testCompile libraries.robolectric
    testCompile libraries.assertJ
    testCompile libraries.equalsVerifier
    testCompile libraries.mockitoCore
}

apply plugin: 'com.google.gms.google-services'
